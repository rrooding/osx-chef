#!/bin/bash

cat <<EOF |
on isUIScriptingEnabled()
  tell application "System Events" to set isEnabled to UI elements enabled
  return isEnabled
end isUIScriptingEnabled

on triggerAccessibilityDialog()
  try
    tell application "System Events"
      tell process "Finder"
        click menu bar 1
      end tell
    end tell
  on error error_message
    # Ignore error, it is only to trigger the OS X 10.9 popup
  end try
end triggerAccessibilityDialog

on run
  if not isUIScriptingEnabled() then
    display dialog "Click OK to configure accessibility permissions"
    triggerAccessibilityDialog()

    log "waiting for accessibility to be enabled"
    repeat while not isUISCriptingEnabled()
      delay 1
    end repeat
  end if

  log "ui scripting enabled"
end run
EOF
osascript

cat <<EOF |
on doMenu(app_name, menu_name, item_name)
  try
    tell application app_name
      activate
    end tell

    tell application "System Events"
      tell process app_name
        tell menu bar 1
          tell menu bar item menu_name
            tell menu menu_name
              click menu item item_name
            end tell
          end tell
        end tell
      end tell
    end tell

    return true
  on error error_message
    log error_message
    return false
  end try
end doMenu

tell application "App Store"
  launch
  set frontmost to true
  delay 1 
end tell

doMenu("App Store", "Store", "Search")

tell application "System Events" to tell process "App Store"
  keystroke "XCode" & return

  tell window "App Store"
    try
      repeat until exists button 1 of group 1 of group 4 of list 1 of group 2 of UI element 1 of scroll area 1
        delay 0.2
      end repeat
      set bName to name of button 1 of group 1 of group 4 of list 1 of group 2 of UI element 1 of scroll area 1
      log bName

      if bName is "FREEFREE" then
        tell group 1 of group 4 of list 1 of group 2 of UI element 1 of scroll area 1
          click button "FREEFREE"
          repeat until exists button "INSTALL APPFREE"
            delay 0.2
          end repeat
          click button "INSTALL APPFREE"
        end tell
      end if
    on error error_message
      return error_message
    end try
  end tell
end tell
EOF
osascript
